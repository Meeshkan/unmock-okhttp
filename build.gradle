plugins {
    id 'java-library'
    id 'maven'
    id 'signing'
}

group 'io.unmock'
version '0.0.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

archivesBaseName = 'okhttp'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'unmock-okhttp'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Core functions with which one can build an unmock java integration'
                url 'http://www.example.com/example-application'

                scm {
                    connection 'https://github.com/unmock/unmock-okhttp.git'
                    developerConnection 'https://github.com/unmock/unmock-okhttp'
                    url 'https://github.com/unmock/unmock-okhttp'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mikesol'
                        name 'Mike Solomon'
                        email 'mike@meeshkan.com'
                    }
                }
            }
        }
    }
}

configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile group: 'io.unmock', name: 'core', version: '0.0.0'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.14.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.json', name: 'json', version: '20180130'
}
